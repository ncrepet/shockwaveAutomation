(* Patched for use with FeynCalc *)


	(* Kinematic indices are `transported' along a propagator line.
	   KinematicIndices[X] = {Name} means that the generic field X
	   will carry an index Index[Name, i] along the line:  
	   X[ n, {m..}, p, {Index[Name, i]} -> {Index[Name, i + 1]} ] *)

KinematicIndices[ F ] = {};
KinematicIndices[ V ] = {Lorentz};

$FermionLines = True

P$NonCommuting = F 

Attributes[ FAMetricTensor ] = Attributes[ FAScalarProduct ] = {Orderless}


FAFourVector/: -FAFourVector[ mom_, mu___ ] := FAFourVector[Expand[-mom], mu]

FAFourVector[ 0, ___ ] = 0


SpinorType[j_Integer, ___] := MajoranaSpinor /; SelfConjugate[F[j]]

SpinorType[_Integer, __] = FADiracSpinor


M$GenericPropagators = {

	(* general fermion propagator: *)

  AnalyticalPropagator[External][ s1 F[j1, mom]] == 
    FANonCommutative[ SpinorType[j1][-mom, Mass[F[j1]]] ] HeavisideTheta[plus[CorrectedMomentum[mom]]] /Sqrt[2*plus[CorrectedMomentum[mom]]],

	(* Remarks: Fermionic propagators have (like all others, too) their
	   momentum flowing from left to right. The fermion flow (for
	   Dirac fermions: fermion number flow) is from right to left.
	   If the fermion inside the propagator has no sign (i.e. fermion
	   number flow is opposite to fermion flow or fermion is self
	   conjugate) we just use the internal propagator S(-p).
	   If the fermion has a sign, we have to use the Feynman rule S(p)
	   according to the Majorana paper. However, this rule is given
	   for a momentum flowing against the fermion flow so, again, we
	   end up with S(-p). *)

  AnalyticalPropagator[Internal][ s1 F[j1, mom] ] ==
    (*FANonCommutative[ FADiracSlash[-mom] ]I /(2*plus[-mom]), *)
    FANonCommutative[FermionNumeratorPropagator[mom]] /(2*plus[CorrectedMomentum[mom]]), 

	(* general vector boson propagator: *)

  AnalyticalPropagator[External][ s1 V[j1, mom, {li2}] ] ==
    FAPolarizationVector[V[j1], mom, li2] HeavisideTheta[plus[mom]] /Sqrt[2*plus[mom]],

  AnalyticalPropagator[Internal][ s1 V[j1, mom, {li1} -> {li2}] ] ==
     (*(*try without ii factor I*)(FAMetricTensor[li1, li2] - (FAFourVector[Sqrt[2]n2,li1]*FAFourVector[mom,li2]+FAFourVector[Sqrt[2]n2,li2]*FAFourVector[mom,li1])/(2 plus[mom]))/(2 plus[mom]),*)
     VectorNumeratorPropagator[CorrectedMomentum[mom],li1,li2]/(2 plus[CorrectedMomentum[mom]]),


	(* general scalar propagator are't changed with respect to Lorentz.gen, scalar fields not supposed to propagate anyway: *)

  AnalyticalPropagator[External][ s1 S[j1, mom] ] == 1,

  AnalyticalPropagator[Internal][ s1 S[j1, mom] ] ==
    I FAPropagatorDenominator[mom, Sqrt[FAGaugeXi[S[j1]]] Mass[S[j1]]]

}

	(* Definition of the generic couplings.
	   The couplings must be defined as a Dot product of the (generic)
	   coupling vector G[+/-][ field1, field2, .. ] and the
	   kinematical vector Gamma = {Gamma1, Gamma2, ...}.
	   The kinematical vector must have the following properties:
	   a) the entries of Gamma must close under permutation of the
	      fields, i.e. under permutation of the momenta and
	      kinematical indices. One exception is allowed: if the
	      elements of Gamma only change their signs under certain
	      permutations (e.g. Gamma1 = mom1 - mom2), a coupling vector
	      G[-] can be used.
	      This leads to the following behaviour during the
	      construction of the classes couplings: if a permuted
	      coupling was found and the corresponding permutation doesn't
	      resolve the coupling vector entry, then the program tries
	      the negative expression of the corresponding Gamma and
	      multiplies the coupling with (-1).
	   b) the entries of the kinematical vector have to be closed
	      under application of the M$FlippingRules, i.e. fermionic
	      couplings have to be written such that the flipped couplings
	      are present in the generic coupling. Again, it is possible
	      to define flippings that change the sign of Gamma and to
	      take care for those signs by using a G[-]. *)

M$GenericCouplings = {

	(* V-V: *)

  AnalyticalCoupling[ s1 V[j1, mom1, {li1}], s2 V[j2, mom2, {li2}] ] ==
    G[1][s1 V[j1], s2 V[j2]] .
      { FAMetricTensor[li1, li2] FAScalarProduct[mom1, mom2],
        FAMetricTensor[li1, li2],
        FAFourVector[mom1, li2] FAFourVector[mom2, li1] },

	(* S-V: *)

  AnalyticalCoupling[ s1 S[j1, mom1], s2 V[j2, mom2, {li2}] ] ==
    G[1][s1 S[j1], s2 V[j2]] .
      { FAFourVector[mom1, li2],
        FAFourVector[mom2, li2] },

	(* S-S: *)

  AnalyticalCoupling[ s1 S[j1, mom1], s2 S[j2, mom2] ] ==
    G[1][s1 S[j1], s2 S[j2]] .
      { FAScalarProduct[mom1, mom2],
        1 },

	(* U-U: *)

  AnalyticalCoupling[ s1 U[j1, mom1], s2 U[j2, mom2] ] ==
    G[-1][s1 U[j1], s2 U[j2]] .
      { FAScalarProduct[mom1, mom2],
        1 },

	(* F-F: *)

  AnalyticalCoupling[ s1 F[j1, mom1], s2 F[j2, mom2] ] ==
    G[-1][s1 F[j1], s2 F[j2]] .
      { FANonCommutative[FADiracSlash[mom1], FAChiralityProjector[-1]],
        FANonCommutative[FADiracSlash[mom2], FAChiralityProjector[+1]],
        FANonCommutative[FAChiralityProjector[-1]],
        FANonCommutative[FAChiralityProjector[+1]] },

	(* V-V-V-V: *)

  AnalyticalCoupling[ s1 V[j1, mom1, {li1}], s2 V[j2, mom2, {li2}], 
      s3 V[j3, mom3, {li3}], s4 V[j4, mom4, {li4}] ] ==
    G[1][s1 V[j1], s2 V[j2], s3 V[j3], s4 V[j4]] .
      { FAMetricTensor[li1, li2] FAMetricTensor[li3, li4],
        FAMetricTensor[li1, li3] FAMetricTensor[li2, li4],
        FAMetricTensor[li1, li4] FAMetricTensor[li3, li2] },

	(* V-V-V: *)

  AnalyticalCoupling[ s1 V[j1, mom1, {li1}], s2 V[j2, mom2, {li2}], 
      s3 V[j3, mom3, {li3}] ] ==
    G[-1][s1 V[j1], s2 V[j2], s3 V[j3]] .
      { FAMetricTensor[li1, li2] FAFourVector[mom2 - mom1, li3] +
          FAMetricTensor[li2, li3] FAFourVector[mom3 - mom2, li1] +
          FAMetricTensor[li3, li1] FAFourVector[mom1 - mom3, li2] },

	(* S-S-S-S: *)

  AnalyticalCoupling[ s1 S[j1, mom1], s2 S[j2, mom2],
      s3 S[j3, mom3], s4 S[j4, mom4] ] ==
    G[1][s1 S[j1], s2 S[j2], s3 S[j3], s4 S[j4]] .
      { 1 },

	(* S-S-S: *)

  AnalyticalCoupling[ s1 S[j1, mom1], s2 S[j2, mom2], s3 S[j3, mom3] ] ==
    G[1][s1 S[j1], s2 S[j2], s3 S[j3]] .
      { 1 },

	(* S-S-V-V: *)

  AnalyticalCoupling[ s1 S[j1, mom1], s2 S[j2, mom2],
      s3 V[j3, mom3, {li3}], s4 V[j4, mom4, {li4}] ] ==
    G[1][s1 S[j1], s2 S[j2], s3 V[j3], s4 V[j4]] .
      { FAMetricTensor[li3, li4] },

	(* S-S-V: *)

  AnalyticalCoupling[ s1 S[j1, mom1], s2 S[j2, mom2],
      s3 V[j3, mom3, {li3}] ] == 
    G[-1][s1 S[j1], s2 S[j2], s3 V[j3]] .
      { FAFourVector[mom1 - mom2, li3] },

	(* S-V-V: *)

  AnalyticalCoupling[ s1 S[j1, mom1], s2 V[j2, mom2, {li2}], 
      s3 V[j3, mom3, {li3}] ] ==
    G[1][s1 S[j1], s2 V[j2], s3 V[j3]] .
      {If[j1===4&&j2==5&&j3==5,(FAMetricTensor[li2, li3]-FAFourVector[Sqrt[2] n1,li2]*FAFourVector[Sqrt[2] n2,li3]/2-FAFourVector[Sqrt[2] n2,li2]*FAFourVector[Sqrt[2] n1,li3]/2)*(plus[mom2]-plus[mom3])*AWilsonLine[{j2},{j2},mom1],FAMetricTensor[li2, li3]] },

	(* F-F-V: *)

  AnalyticalCoupling[ s1 F[j1, mom1], s2 F[j2, mom2],
      s3 V[j3, mom3, {li3}] ] ==
    G[-1][s1 F[j1], s2 F[j2], s3 V[j3]] .
      { FANonCommutative[FADiracMatrix[li3], FAChiralityProjector[-1]], 
        FANonCommutative[FADiracMatrix[li3], FAChiralityProjector[+1]] },
 
	(* F-F-S: *)

  AnalyticalCoupling[ s1 F[j1, mom1], s2 F[j2, mom2], s3 S[j3, mom3] ] ==
    G[1][s1 F[j1], s2 F[j2], s3 S[j3,mom3]] .
      {FANonCommutative[If[j3==4,Gplus,1], FAChiralityProjector[-1]],  
        FANonCommutative[If[j3==4,Gplus,1], FAChiralityProjector[+1]]},

	(* U-U-V: *)

  AnalyticalCoupling[ s1 U[j1, mom1], s2 U[j2, mom2],
      s3 V[j3, mom3, {li3}] ] ==
    G[1][s1 U[j1], s2 U[j2], s3 V[j3]] .
      { FAFourVector[mom1, li3],
        FAFourVector[mom2, li3] },

	(* S-U-U: *)

  AnalyticalCoupling[ s1 S[j1, mom1], s2 U[j2, mom2], s3 U[j3, mom3] ] ==
    G[1][s1 S[j1], s2 U[j2], s3 U[j3]] .
      { 1 }
}

	(* FlippingRules: the flipping rules determines how Dirac
	   objects change when the order of fermion fields in the
	   coupling is reversed. In other words, it defines how the
	   coupling C[F, -F, ...] is derived from C[-F, F, ...].
	   Of the elements of the Dirac algebra we need to consider
	   only gamma_mu omega_pm since the others are either
	   unchanged or not used (sigma_{mu,nu}).
	   See Denner, Eck, Hahn, Kueblbeck, NPB 387 (1992) 467. *)

M$FlippingRules =
  FANonCommutative[dm:_FADiracMatrix | _FADiracSlash, FAChiralityProjector[pm_]] ->
    -FANonCommutative[dm, FAChiralityProjector[-pm]]

	(* TruncationRules: rule for omitting the wave functions of
	   external Propagators defined in this file. *)

M$TruncationRules = {
  _FAPolarizationVector -> 1,
  _FADiracSpinor -> 1,
  _MajoranaSpinor -> 1 
}

	(* LastGenericRules: the very last rules that are applied to an
	   amplitude before it is returned by CreateFeynAmp. *)

M$LastGenericRules = {
  FAPolarizationVector[p_, _. mom:FourMomentum[Outgoing, _], li_] :>
    Conjugate[FAPolarizationVector][p, mom, li],
	CorrectedMomentum[x__] :> Replace[x, -y__ -> y]
}


	(* cosmetics: *)

	(*  left spinor in chain + mom incoming -> \bar v
	    left spinor in chain + mom outgoing -> \bar u
	   right spinor in chain + mom incoming -> u
	   right spinor in chain + mom outgoing -> v *)
Format[
  FermionChain[
    FANonCommutative[_[s1_. mom1_, mass1_]],
    r___,
    FANonCommutative[_[s2_. mom2_, mass2_]]] ] :=
  Overscript[If[FreeQ[mom1, Incoming], "u", "v"], "_"][mom1, mass1] .
    r . If[FreeQ[mom2, Outgoing], "u", "v"][mom2, mass2]

Format[ FADiracSlash ] = "gs"

Format[ FADiracMatrix ] = "ga"

Format[ FAChiralityProjector[1] ] = Subscript["om", "+"]

Format[ FAChiralityProjector[-1] ] = Subscript["om", "-"]

Format[ FAGaugeXi[a_] ] := Subscript["xi", a]

Format[ FAPolarizationVector ] = "ep"

Unprotect[Conjugate];
Format[ Conjugate[a_] ] = Superscript[a, "*"];
Protect[Conjugate]

Format[ FAMetricTensor ] = "g"

Format[ FAScalarProduct[a__] ] := Dot[a]

Format[ FAFourVector[a_, b_] ] := a[b]

Format[ FAFourVector[a_] ] := a

